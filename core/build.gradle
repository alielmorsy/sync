plugins {
    id 'nu.studer.jooq' version '2.0.11'
}

apply plugin: "java"

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets.main.java.srcDirs = [ "src" ]
sourceSets.test.java.srcDirs = [ "test" ]

dependencies {
    jooqRuntime group: 'org.xerial', name: 'sqlite-jdbc', version: '3.30.1'
}

jooq {
    version = '3.9.6'
    edition = 'OSS'
    template(sourceSets.main) {
        jdbc {
            driver = 'org.sqlite.JDBC'
            url = 'jdbc:sqlite:template.sqlite'
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            database {
                name = 'org.jooq.util.sqlite.SQLiteDatabase'
                includes = '.*'
                excludes = 'sqlite_.*'
            }
            target {
                packageName = 'net.sync.game.database.schema'
                directory = 'src'
            }
        }
    }
}

//Enforces generation of the schema sources when task is called
generateTemplateJooqSchemaSource.outputs.upToDateWhen { false }
//Disable generation of the schema on compilation
project.tasks.getByName('compileJava').dependsOn -= 'generateTemplateJooqSchemaSource'
